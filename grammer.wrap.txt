GR_PRIMARY_EXP -> TOKEN_IDENT 
GR_PRIMARY_EXP -> TOKEN_LBRACKET GR_EXP TOKEN_RBRACKET

GR_POSTFIX_EXP -> GR_PRIMARY_EXP 
GR_POSTFIX_EXP -> GR_POSTFIX_EXP TOKEN_LSQUARE GR_EXP TOKEN_RSQUARE 
GR_POSTFIX_EXP -> GR_POSTFIX_EXP TOKEN_LBRACKET TOKEN_RBRACKET 
GR_POSTFIX_EXP -> GR_POSTFIX_EXP TOKEN_LBRACKET GR_ARG_EXP_LIST TOKEN_RBRACKET

GR_ARG_EXP_LIST -> GR_ASSIGN_EXP 
GR_ARG_EXP_LIST -> GR_ARG_EXP_LIST TOKEN_COMMA GR_ASSIGN_EXP

GR_MUL_EXP -> GR_POSTFIX_EXP 
GR_MUL_EXP -> GR_MUL_EXP TOKEN_ASTERISK GR_POSTFIX_EXP 
GR_MUL_EXP -> GR_MUL_EXP TOKEN_SLASH GR_POSTFIX_EXP 
GR_MUL_EXP -> GR_MUL_EXP TOKEN_PERCENTAGE GR_POSTFIX_EXP

GR_ADD_EXP -> GR_MUL_EXP 
GR_ADD_EXP -> GR_ADD_EXP TOKEN_PLUS GR_MUL_EXP 
GR_ADD_EXP -> GR_ADD_EXP TOKEN_MINUS GR_MUL_EXP

GR_REL_EXP -> GR_ADD_EXP 
GR_REL_EXP -> GR_REL_EXP TOKEN_SMALLER GR_ADD_EXP 
GR_REL_EXP -> GR_REL_EXP TOKEN_GREATER GR_ADD_EXP 
GR_REL_EXP -> GR_REL_EXP TOKEN_SEQU GR_ADD_EXP 
GR_REL_EXP -> GR_REL_EXP TOKEN_GEQU GR_ADD_EXP

GR_EQU_EXP -> GR_REL_EXP 
GR_EQU_EXP -> GR_EQU_EXP TOKEN_EQU GR_REL_EXP 
GR_EQU_EXP -> GR_EQU_EXP TOKEN_NEQU GR_REL_EXP

GR_AND_EXP -> GR_EQU_EXP 
GR_AND_EXP -> GR_AND_EXP TOKEN_BAND GR_EQU_EXP

GR_XOR_EXP -> GR_AND_EXP 
GR_XOR_EXP -> GR_XOR_EXP TOKEN_BXOR GR_AND_EXP

GR_OR_EXP -> GR_XOR_EXP 
GR_OR_EXP -> GR_OR_EXP TOKEN_BOR GR_XOR_EXP

GR_LOGICAL_AND_EXP -> GR_OR_EXP 
GR_LOGICAL_AND_EXP -> GR_LOGICAL_AND_EXP TOKEN_AND GR_OR_EXP

GR_LOGICAL_OR_EXP -> GR_LOGICAL_AND_EXP 
GR_LOGICAL_OR_EXP -> GR_LOGICAL_OR_EXP TOKEN_OR GR_LOGICAL_AND_EXP

GR_COND_EXP -> GR_LOGICAL_OR_EXP

GR_ASSIGN_EXP -> GR_COND_EXP
GR_ASSIGN_EXP -> TOKEN_STRING 
GR_ASSIGN_EXP -> GR_POSTFIX_EXP TOKEN_ASSIGN GR_ASSIGN_EXP

GR_EXP -> GR_ASSIGN_EXP    
GR_CONST_EXP -> GR_COND_EXP
GR_DECLARATION -> GR_INIT_DEC_LIST
GR_INIT_DEC_LIST -> GR_INIT_DEC  
GR_INIT_DEC -> GR_DECLARATOR TOKEN_ASSIGN GR_INIT
GR_DECLARATOR -> GR_DIRECT_DEC

GR_DIRECT_DEC -> TOKEN_IDENT 
GR_DIRECT_DEC -> TOKEN_LBRACKET GR_DECLARATOR TOKEN_RBRACKET 
GR_DIRECT_DEC -> GR_DIRECT_DEC TOKEN_LSQUARE GR_CONST_EXP TOKEN_RSQUARE 
GR_DIRECT_DEC -> GR_DIRECT_DEC TOKEN_LSQUARE TOKEN_RSQUARE 
GR_DIRECT_DEC -> GR_DIRECT_DEC TOKEN_LBRACKET GR_PARAM_TYPE_LIST TOKEN_RBRACKET 
GR_DIRECT_DEC -> GR_DIRECT_DEC TOKEN_LBRACKET GR_IDENT_LIST TOKEN_RBRACKET 
GR_DIRECT_DEC -> GR_DIRECT_DEC TOKEN_LBRACKET TOKEN_RBRACKET

GR_PARAM_TYPE_LIST -> GR_PARAM_LIST    

GR_PARAM_LIST -> GR_PARAM_DEC 
GR_PARAM_LIST - GR_PARAM_LIST TOKEN_COMMA GR_PARAM_DEC

GR_PARAM_DEC -> GR_DECLARATOR 
GR_PARAM_DEC -> GR_ABST_DEC   

GR_IDENT_LIST -> TOKEN_IDENT 
GR_IDENT_LIST -> GR_IDENT_LIST TOKEN_COMMA TOKEN_IDENT

GR_ABST_DEC -> GR_DIRECT_ABST_DEC   

GR_DIRECT_ABST_DEC -> TOKEN_LBRACKET GR_ABST_DEC TOKEN_RBRACKET 
GR_DIRECT_ABST_DEC -> TOKEN_LSQUARE TOKEN_RSQUARE 
GR_DIRECT_ABST_DEC -> TOKEN_LSQUARE GR_CONST_EXP TOKEN_RSQUARE 
GR_DIRECT_ABST_DEC -> GR_DIRECT_ABST_DEC TOKEN_LSQUARE TOKEN_RSQUARE 
GR_DIRECT_ABST_DEC -> GR_DIRECT_ABST_DEC TOKEN_LSQUARE GR_CONST_EXP TOKEN_RSQUARE 
GR_DIRECT_ABST_DEC -> TOKEN_LBRACKET TOKEN_RBRACKET 
GR_DIRECT_ABST_DEC -> TOKEN_LBRACKET GR_PARAM_TYPE_LIST TOKEN_RBRACKET
GR_DIRECT_ABST_DEC -> GR_DIRECT_ABST_DEC TOKEN_LBRACKET TOKEN_RBRACKET 
GR_DIRECT_ABST_DEC -> GR_DIRECT_ABST_DEC TOKEN_LBRACKET GR_PARAM_TYPE_LIST TOKEN_RBRACKET 

GR_INIT -> GR_ASSIGN_EXP 
GR_INIT -> GR_ASSIGN_EXP TOKEN_LBRACE GR_INIT_LIST TOKEN_RBRACE 
GR_INIT -> GR_ASSIGN_EXP TOKEN_LBRACE GR_INIT_LIST TOKEN_COMMA TOKEN_RBRACE

GR_INIT_LIST -> GR_INIT 
GR_INIT_LIST -> GR_INIT_LIST TOKEN_COMMA GR_INIT

GR_STATEMENT -> GR_LABELED_STATEMENT 
GR_STATEMENT -> GR_COMPOUND_STATEMENT 
GR_STATEMENT -> GR_EXP_STATEMENT 
GR_STATEMENT -> GR_SELECT_STATEMENT 
GR_STATEMENT -> GR_ITER_STATEMENT 
GR_STATEMENT -> GR_JUMP_STATEMENT

GR_LABELED_STATEMENT -> TOKEN_STRING TOKEN_THEN GR_STATEMENT 
GR_LABELED_STATEMENT -> TOKEN_UNDERSCORE TOKEN_THEN GR_STATEMENT

GR_COMPOUND_STATEMENT -> TOKEN_LBRACE TOKEN_RBRACE 
GR_COMPOUND_STATEMENT -> TOKEN_LBRACE GR_STAT_LIST TOKEN_RBRACE 
GR_COMPOUND_STATEMENT -> TOKEN_LBRACE GR_DEC_LIST TOKEN_RBRACE 
GR_COMPOUND_STATEMENT -> TOKEN_LBRACE GR_DEC_LIST GR_STAT_LIST TOKEN_RBRACE

GR_DEC_LIST -> GR_DECLARATION 
GR_DEC_LIST -> GR_DEC_LIST GR_DECLARATION

GR_STAT_LIST -> GR_STATEMENT 
GR_STAT_LIST -> GR_STAT_LIST GR_STATEMENT

GR_EXP_STATEMENT -> GR_EXP

GR_SELECT_STATEMENT -> TOKEN_IF TOKEN_LBRACKET GR_EXP TOKEN_RBRACKET GR_STATEMENT 
GR_SELECT_STATEMENT -> TOKEN_IF TOKEN_LBRACKET GR_EXP TOKEN_RBRACKET GR_STATEMENT TOKEN_ELSE GR_STATEMENT 
GR_SELECT_STATEMENT -> TOKEN_MATCH TOKEN_LBRACKET GR_EXP TOKEN_RBRACKET GR_STATEMENT

GR_ITER_STATEMENT -> TOKEN_FOR TOKEN_LBRACKET GR_EXP_STATEMENT GR_EXP_STATEMENT TOKEN_RBRACKET GR_STATEMENT 
GR_ITER_STATEMENT -> TOKEN_FOR TOKEN_LBRACKET GR_EXP_STATEMENT GR_EXP_STATEMENT GR_EXP TOKEN_RBRACKET GR_STATEMENT

GR_JUMP_STATEMENT -> TOKEN_CONTINUE 
GR_JUMP_STATEMENT -> TOKEN_BREAK 
GR_JUMP_STATEMENT -> TOKEN_RETURN 
GR_JUMP_STATEMENT -> TOKEN_RETURN GR_EXP

GR_TRANS_UNIT -> GR_EXTERNAL_DEC 
GR_TRANS_UNIT -> GR_TRANS_UNIT GR_EXTERNAL_DEC

GR_EXTERNAL_DEC -> GR_FUNC_DEC 
GR_EXTERNAL_DEC -> GR_DECLARATION

GR_FUNC_DEC -> TOKEN_FUNC GR_DECLARATOR GR_DEC_LIST GR_COMPOUND_STATEMENT 
GR_FUNC_DEC -> TOKEN_FUNC GR_DECLARATOR GR_COMPOUND_STATEMENT